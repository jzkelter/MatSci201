globals [
  r-min
  eps
  -eps*4
  cutoff-dist
  force-offset
  PE-offset
  dt
  kb
]


;;;;;;;;;;;;;;;;;;;;;;
;; Setup Procedures ;;
;;;;;;;;;;;;;;;;;;;;;;

to mdc.setup-constants
  set dt .01
  set r-min  2 ^ (1 / 6)
  set eps 1
  set -eps*4 -4 * eps
  set cutoff-dist 2.5 * r-min
;  ifelse num-atoms = 3 [
;    set cutoff-dist world-width
;  ] [
;    set cutoff-dist 2.5 * r-min
;  ]
end

to mdc.setup-offsets
  set force-offset -1 * mdc.calc-force-without-offset cutoff-dist
  set PE-offset -1 * mdc.calc-pair-PE-without-offset cutoff-dist
end

;to mdc.create-link-rulers
;  ask atoms [
;    ask other atoms [
;      create-link-with myself
;    ]
;  ]
;;  ask links [mdc.set-label-distance]
;end
;
;to mdc.set-label-distance
;  let d [distance [end1] of myself ] of end2
;  set label (word precision (d / r-min) 1 " r0")
;end

to mdc.setup-atoms
  create-atoms num-atoms [
    set shape "circle"
    set color blue
    set mass 1
  ]

  if initial-config = "Solid" [
    let l sqrt(num-atoms) ;the # of atoms in a row
    let x-dist r-min
    let y-dist sqrt (x-dist ^ 2 - (x-dist / 2) ^ 2)
    let ypos (- l * x-dist / 2) ;the y position of the first atom
    let xpos (- l * x-dist / 2) ;the x position of the first atom
    let r-num 0  ;the row number
    ask turtles [  ;set the atoms; positions
      if xpos > (l * x-dist / 2)  [  ;condition to start a new row
        set r-num r-num + 1
        set xpos (- l * x-dist / 2) + (r-num mod 2) * x-dist / 2
        set ypos ypos + y-dist
      ]
      setxy xpos ypos  ;if we are still in the same row
      set xpos xpos + x-dist
    ]
  ]

  if initial-config = "Random" [
    ask atoms [
      setxy random-xcor random-ycor
    ]
    mdc.remove-overlap ;make sure atoms aren't overlapping
  ]

end

to mdc.remove-overlap
  ask atoms [
    while [mdc.overlapping] [
      setxy random-xcor random-ycor
    ]
  ]
end

to-report mdc.overlapping
  report any? other turtles in-radius r-min
end


to mdc.init-velocity
  ask atoms [
    let v-avg sqrt (2 * temp * Kb / mass)
    let a random-float 1  ; a random amount of the total velocity to go the x direction
    set vx sqrt (a * v-avg ^ 2) * mdc.positive-or-negative
    set vy sqrt ( v-avg ^ 2 - vx ^ 2)  * mdc.positive-or-negative
  ]
end


to-report mdc.positive-or-negative
  report ifelse-value random 2 = 0 [-1] [1]
end


;;;;;;;;;;;;;;;;;;;;;;;;
;; Runtime Procedures ;;
;;;;;;;;;;;;;;;;;;;;;;;;

to mdc.simulate
;  ask links [hide-link]
  ask atoms [mdc.move]
  ask atoms [mdc.update-force-and-velocity]
  if constant-temp? [mdc.scale-velocities]
  
  tick-advance dt
  update-plots
end


to mdc.move  ; atom procedure
  ;; Uses velocity-verlet algorithm
  set xcor mdc.velocity-verlet-pos xcor vx (fx / mass)
  set ycor mdc.velocity-verlet-pos ycor vy (fy / mass)
end

to mdc.update-force-and-velocity  ; atom procedure
  let n-fx 0
  let n-fy 0
  ask other atoms in-radius cutoff-dist [
    let r distance myself
    let force mdc.calc-force r
    face myself
    rt 180
    set n-fx n-fx + (force * dx)  ; assuming mass = 1. If not, neeed to divide force by mass to get acceleration
    set n-fy n-fy + (force * dy)
  ]
  set vx mdc.velocity-verlet-velocity vx (fx / mass) (n-fx / mass)
  set vy mdc.velocity-verlet-velocity vy (fy / mass) (n-fy / mass)
  set fx n-fx
  set fy n-fy

end

to mdc.scale-velocities
  let ctemp mdc.current-temp
  (ifelse
    ctemp = 0 and temp != 0 [
      ask atoms [mdc.init-velocity]
    ]
    ctemp != 0 [
      let scale-factor sqrt( temp / ctemp )  ; if "external" temperature is higher atoms will speed up and vice versa
      ask atoms [
        set vx vx * scale-factor
        set vy vy * scale-factor
      ]
    ]
  )
end

to-report mdc.current-temp
  report (1 / (2 * Kb)) * mean [vx ^ 2 + vy ^ 2] of atoms
end


;; *****************************************************
;; ****** Lennard-Jones Potential/Force Procedures *****
;; *****************************************************

to-report mdc.calc-force [r]
  report mdc.calc-force-without-offset r + force-offset
end

to-report mdc.calc-force-without-offset [r]  
  let r^3 r * r * r
  let r^6 r^3 * r^3
  ;report (-eps*4 / (r^6 * r)) * ((1 / r^6) - 1)
  report (-eps*4 * 6 / (r^6 * r)) * ((2 / r^6) - 1)
end


to-report mdc.velocity-verlet-pos [pos v a]  ; position, velocity and acceleration
  report pos + v * dt + (1 / 2) * a * (dt ^ 2)
end

to-report mdc.velocity-verlet-velocity [v a new-a]  ; velocity, previous acceleration, new acceleration
  report v + (1 / 2) * (new-a + a) * dt
end


to-report mdc.calc-PE
  let U 0  ;; U stands for PE

  ask other turtles in-radius cutoff-dist [

    set U U + mdc.calc-pair-PE (distance myself)
  ]
  report U
end

to-report mdc.calc-pair-PE [r]
  report mdc.calc-pair-PE-without-offset r + PE-offset
end

to-report mdc.calc-pair-PE-without-offset [r]
  let rsquare r ^ 2
  let attract-term 1 / rsquare ^ 3
  let repel-term attract-term * attract-term
  ;NOTE could do this a little faster by attract-term * (attract-term -1)
  report 4 * eps * (repel-term - attract-term)
end


;; *****************************************************
;; *********      Interaction Procedures      **********
;; *****************************************************

to mdc.drag-atoms-with-mouse
;  ask links [show-link]
  
  if mouse-down? [
    let close-atoms atoms with [mdc.distance-to-mouse < 0.5]
    if any? close-atoms [
      ask min-one-of close-atoms [mdc.distance-to-mouse] [
        let oldx xcor
        let oldy ycor
        setxy mouse-xcor mouse-ycor
        ifelse mdc.calc-PE > 1 [ ; if energy would be too high, don't let the atom go there.
          setxy oldx oldy
        ] [
          set vx 0
          set vy 0
        ]
      ]
    ]
  ]
  
;  ask links [mdc.set-label-distance]
  display
end

to-report mdc.distance-to-mouse
  report distancexy mouse-xcor mouse-ycor
end