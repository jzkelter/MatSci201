undirected-link-breed [atom-links atom-link] ; links between atoms

globals [
  prev-atom-viz-size  ; previous atom viz size
  LJ-force-cutoff-values  ; since sizes can change, we store the cutoff adjustment values for different sizes of atom pairs
  LJ-pot-cutoff-values  ; since sizes can change, we store the cutoff adjustment values for different sizes of atom pairs
]

;;;;;;;;;;;;;;;;;;;;;;
;; Setup Procedures ;;
;;;;;;;;;;;;;;;;;;;;;;

to ch5.setup-atoms-and-links-and-force-lines
  let x-dist r-min ; the distance between atoms in the x direction
  let y-dist sqrt (x-dist ^ 2 - (x-dist / 2) ^ 2)

  setup-atoms x-dist y-dist
  
  vab.setup-links
end

to ch5.setup-LJ
  set LJ-pot-cutoff-values n-values 41 [0]  ; initialize to zeros so zero as cutoff value will be used in the calculation on next line
  set LJ-force-cutoff-values n-values 41 [0] ; initialize to zeros so zero as cutoff value will be used in the calculation on next line

  set LJ-pot-cutoff-values map  [s -> first ch5.LJ-potential-and-force cutoff-dist s s] (range 0 2.05 .05)  ; calculate cutoff values to adjust LJ potential various sigma values
  set LJ-force-cutoff-values map  [s -> last ch5.LJ-potential-and-force cutoff-dist s s] (range 0 2.05 .05)  ; calculate cutoff values to adjust LJ for various sigma values
end

;setup-atoms somewhere here

to ch5.init-atom
  mdc.init-atom
  
  ; these two lines are for aep, the first is an init, the second is to update the display
  set selected? false
  aep.set-size
  
  ; this is for atom display
  set base-color blue
end


;;*******************************************************
;;**************** Go Procedures ************************
;;*******************************************************



;; *****************************************************
;; *********** Molecular Dynamics Procedures ***********
;; *****************************************************

to ch5.update-force-and-velocity-and-links
  let n-fx 0
  let n-fy 0
  let total-potential-energy 0
  let in-radius-atoms other atoms in-radius cutoff-dist
  ask in-radius-atoms [
    ; each atom calculates the force it feels from its
    ; neighboring atoms and sums these forces
    let r distance myself
    let indiv-pot-E-and-force (ch5.LJ-potential-and-force r sigma [sigma] of myself)
    let force last indiv-pot-E-and-force
    set total-potential-energy total-potential-energy + first indiv-pot-E-and-force
    face myself
    rt 180
    set n-fx n-fx + (force * dx)
    set n-fy n-fy + (force * dy)
    ]
  set pot-E total-potential-energy / 2  ; divide by 2 to not double count pot-E for each atom


  ; updating velocity and force
  if not pinned? [
    set vx mdc.velocity-verlet-velocity vx (fx / mass) (n-fx / mass)
    set vy mdc.velocity-verlet-velocity vy (fy / mass) (n-fy / mass)
    set fx n-fx
    set fy n-fy
  ]

  ch5.update-atom-color pot-E
  
  vab.update-links in-radius-atoms
end


;; *****************************************************
;; ****** Lennard-Jones Potential/Force Procedures *****
;; *****************************************************

to-report ch5.LJ-potential-and-force [ r sigma1 sigma2] ; for the force, positive = attractive, negative = repulsive
  let sig (sigma1 + sigma2) / 2
  let third-power (sig / r) ^ 3
  let sixth-power third-power ^ 2
  let twelfth-power sixth-power ^ 2
  let force (-48 * eps / r ) * (twelfth-power - (1 / 2) * sixth-power) - ch5.LJ-force-cutoff sig
  let potential (4 * eps * (twelfth-power - sixth-power)) - ch5.LJ-pot-cutoff sig
  report list potential force
end


to-report ch5.calc-PE
  let U 0  ;; U stands for PE

  ask other atoms in-radius cutoff-dist [
    set U U + ch5.calc-pair-PE-with myself
  ]
  report U
end

to-report ch5.calc-pair-PE-with [other-atom]
  let PE-and-force ch5.LJ-potential-and-force (distance other-atom) sigma  [sigma] of other-atom
  report first PE-and-force
end


to-report ch5.LJ-force-cutoff [sig]
  report item round (sig / .05) LJ-force-cutoff-values
end

to-report ch5.LJ-pot-cutoff [sig]
  report item round (sig / .05) LJ-pot-cutoff-values
end


;; *****************************************************
;; ********* Atom and Link Display procedures **********
;; *****************************************************

to ch5.update-atom-color [atom-PE] ; updating atom color

  ifelse color-atoms-by-potential-energy? [
    set color scale-color color  atom-PE -6 0
  ] [
    set color base-color
  ]

end


to ch5.update-atom-size-viz
  if atom-viz-size != prev-atom-viz-size [
    ask atoms [aep.set-size]
  ]
  set prev-atom-viz-size  atom-viz-size
end