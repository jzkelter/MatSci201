;; any model that uses this nls file needs to have the following variable declared in atoms-own:
;; ex:
;; atoms-own [
;;   selected?
;; ]

globals [
  prev-atom-viz-size  ; previous atom viz size
  message1 ; this variable holds a turtle for displaying messages.
  message2 ; this variable holds a turtle for displaying messages.
]


;;;;;;;;;;;;;;;;;;;;;;
;; Setup Procedures ;;
;;;;;;;;;;;;;;;;;;;;;;

to aep.init-atom
  set selected? false
  aep.set-size
end

to aep.setup-messages
  crt 1 [
    setxy 2.5 2.5
    set size 0
    set message1 self
  ]
  crt 1 [
    setxy 3 2.25
    set size 0
    set message2 self
  ]
end

;;*******************************************************
;;**************** Go Procedures ************************
;;*******************************************************

to aep.update-atom-size-viz
  if atom-viz-size != prev-atom-viz-size [
    ask atoms [aep.set-size]
  ]
  set prev-atom-viz-size  atom-viz-size
end


;; *****************************************************
;; *********      Interaction Procedures      **********
;; *****************************************************

; ncolor & nsigma taken as input arguments, they are interface elements in Point Defects
to aep.add-atoms [ncolor nsigma]
  if mouse-down? and not any? atoms with [distancexy mouse-xcor mouse-ycor < .2] [
    let closest-atom min-one-of atoms [distancexy mouse-xcor mouse-ycor]
    let new-atom-force last [ch5.LJ-potential-and-force (distancexy mouse-xcor mouse-ycor) sigma nsigma] of closest-atom

    ifelse abs new-atom-force < 30 [

      create-atoms 1 [
        mdc.init-atom
        aep.init-atom
        set sigma nsigma
        set mass sigma ^ 2  ; mass is proportional to radius squared (because in 2D)
        aep.set-size
        set base-color read-from-string ncolor
        set color base-color
        setxy mouse-xcor mouse-ycor
      ]
      wait 0.1
    ] [
      ask message1 [set label "Adding that atom there will make things explode!"]
      ask message2 [set label "(if you are very precise it is possible to add an interstitial)"]
      display
      wait 1
      ask message1 [set label ""]
      ask message2 [set label ""]
    ]
  ]
end


to aep.delete-atoms
  if mouse-down? [
    ask atoms with [xcor <= mouse-xcor + .5 and xcor > mouse-xcor - .5
      and ycor <= mouse-ycor + .433 and ycor > mouse-ycor - .433 ] [die]
  ]

end


to aep.select-atoms
  if mouse-down? [
    ask atoms with [distancexy mouse-xcor mouse-ycor < (1 / 2)] [
      set selected? not selected?
      aep.set-shape
    ]
    wait 0.1
  ]
end

to aep.set-shape
  ifelse selected? [
    ;set shape "circle 2"
    set shape "circle-s"
  ] [
    set shape "circle"
  ]
end


to aep.change-atom-size [change]
  ask atoms with [selected?] [
    set sigma max list 0.2 (sigma + change)
    set mass sigma ^ 2  ; mass is proportional to radius squared (because in 2D)
    aep.set-size
  ]
end


to aep.set-size
  set size sigma * atom-viz-size
end